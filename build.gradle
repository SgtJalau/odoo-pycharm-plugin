plugins {
    id 'java'
    // get the newest version from https://jetbrains.org/intellij/sdk/docs/tutorials/build_system/prerequisites.html
    id 'org.jetbrains.intellij' version '1.0'
}

group 'at.wtioit.intellij.plugins'
version '0.6.0-SNAPSHOT'

sourceCompatibility = 1.8

def intellijVersion = System.getenv("AT_WTIOIT_PYCHARM_PLUGIN_testVersion") ?: '2019.2'
def intellijType = System.getenv("AT_WTIOIT_PYCHARM_PLUGIN_testType") ?: 'PC'
def intellijPlugins = []

if (intellijVersion != '2019.2' && intellijType == 'PC') {
    // only pycharm 2019.2 seems to provide com.jetbrains.python.psi itself
    intellijPlugins.add('PythonCore')
    // see https://plugins.jetbrains.com/plugin/7322-python-community-edition/versions
} else if (intellijType == 'PY') {
    // PyCharm Professional uses another python plugin
    intellijPlugins.add('Pythonid')
    // see https://plugins.jetbrains.com/plugin/631-python/versions
}

repositories {
    mavenCentral()
}

dependencies {
    //testCompile group: 'junit', name: 'junit', version: '4.12'
    //testCompile group: 'org.mockito', name: 'mockito-core', version: '2.28.2'
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    version.set(intellijVersion)
    type.set(intellijType)
    plugins.set(intellijPlugins)
}

runIde {
    // enable DebugAttachDetector
    jvmArgs '--add-exports', 'java.base/jdk.internal.vm=ALL-UNNAMED', '-Xmx4G'
    //jvmArgs '--add-exports', 'java.base/jdk.internal.vm=ALL-UNNAMED', '-agentpath:/home/bigbear3001/bin/YourKit-JavaProfiler-2020.7/bin/linux-x86-64/libyjpagent.so=disablestacktelemetry,exceptions=disable,delay=10000'

}

patchPluginXml {
    // please see http://www.jetbrains.org/intellij/sdk/docs/basics/getting_started/build_number_ranges.html for description
    sinceBuild.set("192.5728")
    untilBuild.set(null)
}



publishPlugin {
    token.set(System.getenv("AT_WTIOIT_PYCHARM_PLUGIN_intellijPublishToken"))
}